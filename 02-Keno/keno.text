PROGRAM KENO(INPUT, OUTPUT);
USES APPLESTUFF;

CONST
  PAYOFF5        = 10;          (* FOR 5 SPOTS PLAYER GETS $10 *)
  PAYOFF6        = 100;         (* FOR 5 SPOTS PLAYER GETS $100 *)
  PAYOFF7        = 2200;        (* FOR 5 SPOTS PLAYER GETS $2200 *)
  PAYOFF8        = 25000;       (* FOR 5 SPOTS PLAYER GETS $25000 *)
  TOTALSPOTS     = 80;          (* TOTAL OF 80 POSSIBLE SPOTS *)
  NUMCOMPSPOTS   = 20;          (* COMPUTER PICKS 20 SPOTS *)
  NUMPLAYERSPOTS = 8;           (* PLAYER GETS 8 SPOTS *)
  COST           = 1.20;        (* COST TO PLAY A GAME *)

TYPE
  SPOTTYPE = SET OF 1..TOTALSPOTS;

VAR
  COMPSPOTS   : SPOTTYPE;       (* SPOTS THE COMPUTER CHOOSES *)
  PLAYERSPOTS : SPOTTYPE;       (* PLAYER'S SPOTS *)
  MONEY       : REAL;           (* PLAYER HAS THIS MUCH MONEY *)
  CH          : CHAR;           (* INPUT TO SEE IF USER WISHES TO CONTINUE *)


(*
 * GETSPOTS
 * GET THE PLAYER'S 8 SPOTS FROM HIM
 *)
PROCEDURE GETSPOTS;
VAR
  I : 0..NUMPLAYERSPOTS;    (* NUMBER OF SPOTS PLAYER HAS CHOSEN SO FAR *)
  SPOT : INTEGER;           (* NUMBER JUST CHOSEN *)
BEGIN
  I := 0;
  PLAYERSPOTS := [];
  WHILE I < NUMPLAYERSPOTS DO
  BEGIN
    RANDOMIZE;
    WRITE('YOUR SPOT: ');
    READLN(SPOT);
    IF (SPOT < 1) OR (SPOT > TOTALSPOTS) THEN
      WRITELN('ILLEGAL SPOT NUMBER')
    ELSE
      IF SPOT IN PLAYERSPOTS THEN
        WRITELN('YOU HAVE ALREADY CHOSEN SPOT ',SPOT)
      ELSE
      BEGIN
        I := I + 1;
        PLAYERSPOTS := PLAYERSPOTS + [SPOT]
      END
  END
END;


(*
 * COMPUTER
 * HAVE THE COMPUTER PICK ITS SPOTS
 *)
PROCEDURE COMPUTER;
VAR
  I : 0..NUMCOMPSPOTS;      (* NUMBER OF SPOTS PLAYER HAS CHOSEN SO FAR *)
  SPOT : 0..TOTALSPOTS;
  
BEGIN
  I := 0;
  COMPSPOTS := [];
  WRITELN('THE COMPUTER CHOOSES ');
  WHILE I < NUMCOMPSPOTS DO
  BEGIN
    SPOT := 1 + (RANDOM MOD TOTALSPOTS);
    IF NOT (SPOT IN COMPSPOTS) THEN
    BEGIN
      COMPSPOTS := COMPSPOTS + [SPOT];
      I := I + 1;
    END
  END;

  (*
   * PRINT OUT THE COMPUTER'S SPOTS IN
   * ORDER BY GOING LINERARLY THROUGH
   * ALL NUMBERS
   *)
  I := 0;
  SPOT := 0;
  WHILE I < NUMCOMPSPOTS DO
  BEGIN
    SPOT := SPOT + 1;
    IF SPOT IN COMPSPOTS THEN
    BEGIN
      WRITE(SPOT:3);
      I := I + 1;
      IF I = 10 THEN
        WRITELN
    END
  END;
  WRITELN
END;


(*
 * SCORE
 * FIND OUT HOW MUCH (IF ANYTHING) THE
 * PLAYER HAS WON
 *)
PROCEDURE SCORE;
VAR
  I : 0..NUMPLAYERSPOTS;    (* NUMBER OF MATCHES MADE *)
  SPOT : 0..TOTALSPOTS;     (* CURRENT SPOT NUMBER *)
BEGIN
  MONEY := MONEY - COST;
  I := 0;
  FOR SPOT := 1 TO TOTALSPOTS DO
    IF (SPOT IN COMPSPOTS) AND (SPOT IN PLAYERSPOTS) THEN
      I := I + 1;
  IF I IN [5,6,7,8] THEN
  BEGIN
    WRITE('YOU LUCKY PERSON, YOU HAVE ',I);
    WRITELN(' MATCHES');
    WRITE('THAT MEANS YOU HAVE MADE ');
    CASE I OF
      5:
          BEGIN
            WRITELN(PAYOFF5);
            MONEY := PAYOFF5 + MONEY
          END;
      6:
          BEGIN
            WRITELN(PAYOFF6);
            MONEY := PAYOFF6 + MONEY
          END;
      7:
          BEGIN
            WRITELN(PAYOFF7);
            MONEY := PAYOFF7 + MONEY
          END;
      8:
          BEGIN
            WRITELN(PAYOFF8);
            MONEY := PAYOFF8 + MONEY
          END
    END
  END
  ELSE
    WRITELN('SORRY, BUT YOU ONLY MATCHED ',I);

  WRITELN;
  IF MONEY >= 0 THEN
    WRITELN('YOUR TOTAL MONEY IS $',MONEY:4:2)
  ELSE
    WRITELN('SO FAR YOU HAVE LOST $',ABS(MONEY):4:2);
  WRITELN
END;


BEGIN
  MONEY := 0;
  WHILE CH <> 'N' DO
  BEGIN
    GETSPOTS;
    COMPUTER;
    SCORE;
    WRITELN;
    WRITE('WANT TO PLAY AGAIN? ');
    READLN(CH)
  END;
  WRITELN;
  WRITELN('ALL RIGHT THEN LEAVE, SEE IF I CARE!!!');
  IF (MONEY >= 0) THEN
    WRITELN('YOU MADE $',MONEY:5:2)
  ELSE
    WRITELN('YOU LOST $',ABS(MONEY):5:2)
END. (* KENO, 7/5/81 D.H. *)
