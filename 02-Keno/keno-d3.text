(*
 * KENO
 * Welcome to the Keno lounge.
 *)
PROGRAM Keno(INPUT, OUTPUT);
USES AppleStuff;

CONST
  MINMATCHES    = 3; (*FIXME*)  (* Minimum spot matches for a win *)
  NUMPLAYSPOTS  = 8;            (* Player gets 8 spots *)
  NUMCOMPSPOTS  = 20;           (* Computer picks 20 spots *)
  TOTALSPOTS    = 80;           (* Total of 80 possible spots *)

  COST          = 1.20;         (* Cost to play a game *)
  PAYOFF5       = 10;           (* For 5 spots player gets $10 *)
  PAYOFF6       = 100;          (* For 5 spots player gets $100 *)
  PAYOFF7       = 2200;         (* For 5 spots player gets $2200 *)
  PAYOFF8       = 25000;        (* For 5 spots player gets $25000 *)

  CH_FF         = 12;           (* ASCII form feed, clears screen *)
TYPE
  SpotType = SET of 1..TOTALSPOTS;

VAR
  Ch        : Char;             (* Input to see if user wishes to continue *)
  CompSpots : SpotType;         (* Spots the computer chooses *)
  PlaySpots : SpotType;         (* Player's spots *)
  Matches   : 0..NUMPLAYSPOTS;  (* Player matched computer this many times *)
  Money     : Real;             (* Player has this much money *)
  PayOffs   : Array [MINMATCHES..NUMPLAYSPOTS] of Real;


(*
 * INITIALIZE
 * Sets up game and finances
 *)
PROCEDURE Initialize;
BEGIN
  Ch := 'Y';
  Money := 0.00;
  PayOffs[3] := 1.20;       PayOffs[6] := 2.40;    
  PayOffs[5] := PAYOFF5;    PayOffs[6] := PAYOFF6;
  PayOffs[7] := PAYOFF5;    PayOffs[8] := PAYOFF8;
END; (* Initialize *)


(*
 * GETSPOTS
 * Get the player's 8 spots from him
 *)
PROCEDURE GetSpots;
VAR
  I : 0..NUMPLAYSPOTS;      (* Number of spots player has chosen so far *)
  Spot : Integer;           (* Number just chosen *)
BEGIN
  I := 0;
  Matches := 0;
  PlaySpots := [];
  While I < NUMPLAYSPOTS Do
  Begin
    Randomize;
    Write('Your spot: ');
    ReadLn(Spot);
    If (Spot < 1) OR (Spot > TOTALSPOTS) Then
      WriteLn('Illegal spot number')
    Else
      If Spot IN PlaySpots Then
        WriteLn('You have already chosen spot ',Spot)
      Else
      Begin
        I := I + 1;
        PlaySpots := PlaySpots + [Spot]
      End
  End
END; (* GetSpots *)


(*
 * COMPUTER
 * Have the computer pick its spots
 *)
PROCEDURE Computer;
VAR
  I : 0..NUMCOMPSPOTS;      (* Number of spots player has chosen so far *)
  Spot : 0..TOTALSPOTS;
  
BEGIN
  I := 0;
  CompSpots := [];
  WriteLn('The computer chooses ');
  While I < NUMCOMPSPOTS Do
  Begin
    Spot := 1 + (Random MOD TOTALSPOTS);
    If NOT (Spot IN CompSpots) Then
    Begin
      (* Not a dup; record this spot for the computer *)
      CompSpots := CompSpots + [Spot];
      I := I + 1;
      (* Check if player has a match. Note we would have fewer checks
       * if the computer went first, but that's not normal game play *)
      If (Spot IN PlaySpots) Then
        Matches := Matches + 1;
    End
  End;

  (* Print out the computer's spots in order *)
  I := 0;
  Spot := 0;
  While I < NUMCOMPSPOTS Do
  Begin
    Spot := Spot + 1;
    If Spot in CompSpots Then
    Begin
      Write(Chr(Ord('A')-64));
      Write(Spot:3);
      I := I + 1;
      If I = 10 Then
        WriteLn
    End
  End;
  WriteLn
END; (* Computer *)


(*
 * SCORE
 * Find out how much (if anything) the player has won
 *)
PROCEDURE Score;
BEGIN
  Money := Money - COST;

  If Matches >= MIN_MATCHES Then
  Begin
    WriteLn('You lucky person, you have ',Matches,' matches.');
    WriteLn('That means you have made $',PayOffs[Matches]:5:0,'.');
    Money := Money + PayOffs[Matches]
  End
  Else If Matches >= 1 Then
    WriteLn('Sorry, but you only matched ',Matches,'.')
  Else
    WriteLn('Sorry. You did''t match any!');

  WriteLn;
  If Money >= 0.00 Then
    WriteLn('Your total money is $',Money:4:2)
  Else
    WriteLn('So far you have lost $',-Money:4:2);
  WriteLn
END; (* Score *)


(*
 * MAIN
 *)
BEGIN
  Initialize;
  While (Ch <> 'N') AND (ch <>  'n') Do
  Begin
    WriteLn(Chr(CH_FF),'Welcome to the Keno Lounge');
    GetSpots;
    Computer;
    Score;
    WriteLn;
    Write('Want to play again? ');
    ReadLn(Ch)
  End;
  WriteLn;
  WriteLn('All right then leave, see if I care!!!');
  If (Money >= 0) Then
    WriteLn('You made $',Money:5:2)
  Else
    WriteLn('You lost $',Abs(Money):5:2)
END. (* Keno: 5-jul-1981 D.H.; 5-aug-2024 d**3 *)
