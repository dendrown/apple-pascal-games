PROGRAM OVERUNDER(INPUT, OUTPUT);
USES APPLESTUFF;

CONST
  MAXDIE        = 6;            (* MAXIMUM # ON A DIE *)
  MINDIE        = 1;            (* MINIMUM # ON A DIE *)

  MAXSUM        = 12;           (* MAXIMUM SUM FOR TWO DICE *)
  MINSUM        = 2;            (* MINIMUM SUM FOR TWO DICE *)

  STARTMONEY    = 500;          (* AMOUNT OF MONEY PLAYER STARTS WITH *)

TYPE
  DIESUM    = MINDIE..MAXSUM;   (* TYPE FOR THE SUM OF DICE *)
  DIETYPE   = MINDIE..MAXDIE;   (* A DIE VALUE *)

VAR
  MONEY     : INTEGER;          (* HOW MUCH MONEY THE PLAYER HAS *)
  BET       : INTEGER;          (* HOW MUCH IS BEING BET THIS TIME *)
  DIE       : DIETYPE;          (* VALUE OF A DIE *)
  SUM       : DIESUM;           (* SUM OF THE TWO DICE *)
  HUMNUM    : DIESUM;           (* NUMBER PICKED BY THE USER *)
  CH        : CHAR;             (* USED TO ANSWER QUESTIONS *)


(*
 * PLAY
 * THROWS THE DICE, PRINTS THEM OUT,
 * AND KEEPS SCORE.
 *)
PROCEDURE PLAY;

  (*
   * DIEROLL
   * RETURNS A RANDOM DIE VALUE
   *)
  FUNCTION DIEROLL : DIETYPE;
  BEGIN
    DIEROLL := 1 + RANDOM MOD MAXDIE
  END; (* DIEROLL *)

BEGIN
  WRITELN('DIE#1     DIE#2     SUM     YOUR#   ROLL');
  DIE := DIEROLL;
  WRITE(DIE:4,'     ');
  SUM := DIE;
  DIE := DIEROLL;
  SUM := SUM + DIE;
  WRITE(DIE:4,'     ',SUM:4,'    ',HUMNUM:4,'      ');
  IF SUM < 7 THEN
    WRITELN('UNDER')
  ELSE
    IF SUM > 7 THEN
      WRITELN('OVER')
    ELSE  
      WRITELN('EVEN');

  IF SUM = HUMNUM THEN
  BEGIN
    WRITELN('YOU MATCHED!!!!');
    WRITELN('YOU GET $',BET*4);
    MONEY := MONEY + (BET * 4);
  END
  ELSE
    IF ((SUM < 7) AND (HUMNUM < 7)) OR ((SUM > 7) AND (HUMNUM > 7)) THEN
    BEGIN
      WRITELN('YOU MADE EVEN MONEY');
      WRITELN('YOU WON $',BET);
      MONEY := MONEY + BET;
    END
    ELSE
    BEGIN
      WRITELN('YOU LOST $',BET);
      MONEY := MONEY - BET;
    END;

  WRITELN('YOUR NEW TOTAL IS $',MONEY)
END; (* PLAY *)


(*
 * GETNUMBER
 * GETS A NUMBER BETWEEN MIN AND
 * MAX FROM THE USER, WITH PROPER ERROR
 * CHECKING
 *)
PROCEDURE GETNUMBERS;
  (*
   * LEGAL
   * RETURNS A RANDOM DIE VALUE
   *)
  FUNCTION LEGAL(QUESTION : STRING; MIN, MAX : INTEGER) : INTEGER;
  VAR
    NUM : INTEGER;          (* INPUT NUMBER *)
  BEGIN
    NUM := -1;

    WHILE (NUM < MIN) OR (NUM > MAX) DO
    BEGIN
      WRITE(QUESTION);
      RANDOMIZE;
      GET(INPUT);           (* SET INPUT *)
      WHILE NOT (INPUT^ IN ['1'..'9']) DO
      BEGIN
        READLN(INPUT);      (* SNARF GARBAGE *)
        WRITELN('SORRY, BUT ONLY NUMBERS ARE ALLOWED');
        WRITE(QUESTION);
        GET(INPUT)
      END;
      READLN(NUM);
      IF (NUM < MIN) OR (NUM > MAX) THEN
        WRITELN('SORRY, ONLY NUMBERS BETWEEN ',MIN,' AND ',MAX)
    END;
    LEGAL := NUM
  END; (* LEGAL *)

BEGIN
  HUMNUM := LEGAL('WHAT NUMBER DO YOU WANT? ', MINSUM, MAXSUM);
  BET := LEGAL('YOUR BET? ', 1, MONEY)
END; (* GETNUMBERS *)


(*
 * INST
 * PRINT OUT A LIST OF INSTRUCTIONS
 *)
PROCEDURE INST;
BEGIN
  WRITELN('OVERUNDER');
  WRITELN('A SIMPLE DICE GAME.');
  WRITELN('YOU CHOOSE A NUMBER BETWEEN 2 AND 12.');
  WRITELN('IF THE SUM OF TWO DICE ROLLED IS THE SAME');
  WRITELN('AS THE NUMBER YOU PICKED, YOU WIN FOUR ');
  WRITELN('TIMES YOUR BET. IF THE NUMBER, AND THE ');
  WRITELN('DICE SUM ARE EITHER BOTH UNDER, OR BOTH');
  WRITELN('OVER 7, THEN YOU WIN THE AMOUNT YOU BET.');
  WRITELN;
  WRITELN
END; (* INST *)


BEGIN
  MONEY := STARTMONEY;
  WRITE('DO YOU WANT INSTRUCTIONS? ');
  READLN(CH);
  IF CH = 'Y' THEN
    INST;
  REPEAT
    WRITELN;
    GETNUMBERS;
    WRITELN;
    PLAY;
    WRITELN;
    WRITE('WANT TO TRY AGAIN? ');
    READLN(CH);
  UNTIL (CH <> 'Y') OR (MONEY <= 0);
  IF MONEY <= 0 THEN
    WRITELN('SORRY, YOU''RE OUT OF MONEY')
END.
